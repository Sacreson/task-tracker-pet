plugins {
    // apply false = мы скачиваем плагин, но НЕ активируем его для корневой папки.
    // Это предотвратит попытку создать bootJar из корня.
    id 'org.springframework.boot' version '3.4.0' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
    id 'java' // Плагин Java для корня тоже можно, но сборку jar мы отключим ниже
}

// allprojects = настройки для КОРНЯ + ВСЕХ подмодулей
allprojects {
    group = 'com.sacreson'
    version = '0.0.1-SNAPSHOT'

    //  репозиторий виден везде, включая корень
    repositories {
        mavenCentral()
    }
}

// subprojects = настройки ТОЛЬКО для подмодулей (api, scheduler, email...)
subprojects {
    apply plugin: 'java'
    // Активируем Spring Boot только для детей
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '17'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}


jar {
    enabled = false
}

tasks.named('wrapper') {
    gradleVersion = '8.5'
    distributionType = Wrapper.DistributionType.BIN
}